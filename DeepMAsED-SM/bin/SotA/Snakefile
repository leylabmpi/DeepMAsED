rule ALE:
    """
    Running ALE on assemblies
    """
    input:
        ref = asmbl_dir + '{rep}/{assembler}/contigs_filtered.fasta',
        bam = map_dir + '{rep}/{assembler}.bam',
        bai = map_dir + '{rep}/{assembler}.bam.bai'
    output:
        txt = ale_dir + '{rep}/{assembler}/ALE_results.txt'
    params:
        params = config['params']['ALE'],
        exe = config['pipeline']['script_folder'] + 'ALE'
    resources:
        time = lambda wildcards, attempt: attempt * 59 * 12,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 + 7
    log:
        log_dir + 'ALE/{rep}/{assembler}.log'
    benchmark:
        benchmark_dir + 'ALE/{rep}/{assembler}.txt'
    shell:
        """
        {params.exe} {params.params} {input.bam} {input.ref} {output.txt} 2> {log}
        """

rule VALET:
    """
    Running VALET on assemblies
    """
    input:
        ref = asmbl_dir + '{rep}/{assembler}/contigs_filtered.fasta',
	read1 = config['tmp_dir'] + '{rep}/sim_reads/1/R1.fq',
	read2 = config['tmp_dir'] + '{rep}/sim_reads/1/R2.fq'
    output:
        sum = valet_dir + '{rep}/{assembler}/summary.tsv',
        sbed = valet_dir + '{rep}/{assembler}/summary.bed',
        ssbed = valet_dir + '{rep}/{assembler}/suspicious.bed',
        cbed = valet_dir + '{rep}/{assembler}/coverage.bed',
        bbed = valet_dir + '{rep}/{assembler}/breakpoints.bed'
    params:
        params = config['params']['VALET'],
        tmp_dir = config['tmp_dir'] + 'VALET/{rep}/{assembler}/valet_out/reference/'
    threads:
        8
    resources:
        time = lambda wildcards, attempt: attempt * 60 * 12,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 + 2
    conda:
        '../envs/valet.yaml'
    log:
        log_dir + 'VALET/{rep}/{assembler}.log'
    benchmark:
        benchmark_dir + 'VALET/{rep}/{assembler}.txt'
    shell:
        """
        # tmp directory
        TMPDIR=`dirname {params.tmp_dir}`
        rm -rf $TMPDIR 2> {log}

        # valet run
        valet.py --threads {threads} \
          --assembly-names reference \
          --skip-reapr {params.params} \
          --output-dir $TMPDIR -a {input.ref} \
          -1 {input.read1} -2 {input.read2} \
          2>> {log}        

        # copying files
        cp -f {params.tmp_dir}/summary.tsv {output.sum} 2>> {log}
        cp -f {params.tmp_dir}/summary.bed {output.sbed} 2>> {log}
        cp -f {params.tmp_dir}/suspicious.bed {output.ssbed} 2>> {log}
        cp -f {params.tmp_dir}/coverage.bed {output.cbed} 2>> {log}
        cp -f {params.tmp_dir}/breakpoints.bed {output.bbed} 2>> {log}
        
        # removing tmp dir
        rm -rf $TMPDIR 2>> {log}
        """
